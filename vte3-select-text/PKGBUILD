# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=vte3-select-text
pkgver=0.38.2
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
depends=('gtk3' 'vte-common')
makedepends=('intltool' 'gobject-introspection' 'gtk3' 'python2' 'vala')
url="http://www.gnome.org"
source=(http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz
        expose_select_text.patch)
sha256sums=('b2daba79229c84de3fbc41fcbb160c8d7ca50523fc01dedefada27e75ecc7de5'
            '3389d31b69740fed3b658ca07d89b77b78ab4fbb9fe4786951c186ba889cb524')
provides=(vte3)
conflicts=(vte3)

prepare() {
  cd "vte-$pkgver"
  patch -p1 -i ../expose_select_text.patch
}

build() {
  cd "vte-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/vte \
    --localstatedir=/var --disable-static \
    --enable-introspection --enable-gnome-pty-helper
  make
}

package() {
  cd "vte-$pkgver"
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/etc/profile.d/vte.sh"
  rm "$pkgdir/usr/lib/vte/gnome-pty-helper"
}
