# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=vte3-select-text
pkgver=0.35.2
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
depends=('gtk3' 'vte-common')
makedepends=('intltool' 'gobject-introspection' 'gtk3')
url="http://www.gnome.org"
source=(http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz
        expose_select_text.patch)
sha256sums=('9d671ace35c56bf30c0588f5b49871ab0cb5ca6f3bb6c549f9936352a5c88848'
            '37fc0ecd4939c3b14f36dace31b54507e0f1cf1fc95a07ae079b1997d0481d7e')
provides=(vte3)
conflicts=(vte3)

build() {
  cd "vte-$pkgver"
  patch -p1 -i ../expose_select_text.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/vte \
    --localstatedir=/var --disable-static \
    --enable-introspection
  make
}

package() {
  cd "vte-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/lib/vte/gnome-pty-helper
}
