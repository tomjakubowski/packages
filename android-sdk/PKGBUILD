# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
pkgname=android-sdk
pkgver=r22.6.2
pkgrel=1
pkgdesc='Google Android SDK'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
depends=('java-runtime' 'alsa-lib' 'openal' 'libstdc++5' 'libxv'
         'ncurses' 'sdl' 'swt' 'zlib')
if [[ $CARCH = x86_64 ]]; then
  depends=('java-runtime' 'lib32-alsa-lib' 'lib32-openal' 'lib32-libstdc++5'
           'lib32-libxv' 'lib32-ncurses' 'lib32-sdl' 'lib32-zlib' 'swt')
fi
install=$pkgname.install
optdepends=('android-udev: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(https://dl.google.com/android/${pkgname}_$pkgver-linux.tgz
        $pkgname.png
        $pkgname.desktop
        $pkgname.sh
        $pkgname.csh
        $pkgname.conf
        license.html)
md5sums=('ff1541418a44d894bedc5cef10622220'
         '1379235d0b536096e08ffd162d0feea7'
         'bae60e64d5f3cab67f01a5b38c44d94b'
         'bb600506a10eda2cce7f4f72e66825fd'
         '5cce59de06290eb7779ecbb3c4a2a748'
         '470daf5b7595a30f18ffccc1139e3ff5'
         '8bffc1a9d6f1c9a6713ff5a329808047')

package() {
  cd "$pkgdir"
  install -Dm755 "$srcdir/$pkgname.sh" etc/profile.d/$pkgname.sh
  install -Dm755 "$srcdir/$pkgname.csh" etc/profile.d/$pkgname.csh
  install -Dm644 "$srcdir/$pkgname.conf" etc/ld.so.conf.d/$pkgname.conf
  mkdir -p opt
  cp -a "$srcdir/$pkgname-linux" opt/$pkgname
  install -Dm644 "$srcdir/$pkgname.png" usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir/$pkgname.desktop" \
    usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir/license.html" \
    usr/share/licenses/$pkgname/license.html

  if [[ $CARCH = i686 ]]; then
    rm -rf ${pkgdir}/opt/android-sdk/tools/lib/{monitor-,}x86_64 \
      ${pkgdir}/opt/android-sdk/tools/lib/lib64* \
      ${pkgdir}/opt/android-sdk/tools/emulator64-*
  fi

  # Fix broken permissions
  chmod -R o=g opt/$pkgname
}
